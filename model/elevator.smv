-- author: Hendrik Werner s4549775
-- NuSMV model of an elevator.

MODULE Button(door)
	VAR
		pressed: boolean;
		active: boolean;
	ASSIGN
		init(pressed) := FALSE;
		init(active) := FALSE;
		next(pressed) := {TRUE, FALSE};
		next(active) := case
			door.open: FALSE;
			!door.open: active | pressed;
		esac;

MODULE Light(button)
	VAR
		active: boolean;
	ASSIGN
		init(active) := FALSE;
	INVAR button.active <-> active

MODULE Door()
	VAR
		open: boolean;
	ASSIGN
		init(open) := FALSE;

MODULE Elevator(button0, button1, button2, button3)
	VAR
		floor: 0..3;
	ASSIGN
		init(floor) := 3;
		next(floor) := case
			button0.active |
			button1.active |
			button2.active |
			button3.active: (floor + 1) mod 4;
			TRUE: floor;
		esac;

MODULE main
	VAR
		door0: Door();
		door1: Door();
		door2: Door();
		door3: Door();
		button0: Button(door0);
		button1: Button(door1);
		button2: Button(door2);
		button3: Button(door3);
		elevator: Elevator(button0, button1, button2, button3);
		light0: Light(button0);
		light1: Light(button1);
		light2: Light(button2);
		light3: Light(button3);

	-- Every floor is visited infinitely often
	CTLSPEC AG EF elevator.floor = 0
	CTLSPEC AG EF elevator.floor = 1
	CTLSPEC AG EF elevator.floor = 2
	CTLSPEC AG EF elevator.floor = 3

	-- Lights are on iff the corresponding button is pressed
	LTLSPEC G (button0.active <-> light0.active)
	LTLSPEC G (button1.active <-> light1.active)
	LTLSPEC G (button2.active <-> light2.active)
	LTLSPEC G (button3.active <-> light3.active)
	CTLSPEC AG (button0.active <-> light0.active)
	CTLSPEC AG (button1.active <-> light1.active)
	CTLSPEC AG (button2.active <-> light2.active)
	CTLSPEC AG (button3.active <-> light3.active)
